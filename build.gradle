buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.2.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'

configurations { 
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

jar {
    baseName = 'gs-rest-service'
    version = '0.1.0'
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'gradle.cucumber', 'src/test/resources']
        }
    }
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'com.jayway.restassured:rest-assured:2.9.0'
    testCompile 'info.cukes:cucumber-core:1.2.4' 
    testCompile 'info.cukes:cucumber-java:1.2.4'
    testCompile 'info.cukes:cucumber-junit:1.2.4'
    testCompile 'junit:junit:4.12'
    testCompile('com.github.tomakehurst:wiremock:2.4.1')
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

test {
    testLogging {
        events 'started', 'passed'
    }
}

